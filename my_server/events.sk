# fix gold transfers from "pvp" world to "main"

on death:
    if victim is a player:
        add 1 to {kills::%attacker%}
        add 3 to {gold::%attacker%}
        set {status_timer::%victim%} to 0

on join:
    teleport player to location 0.5, -60, 0.5 in world "main"
    set the join message to "<light cyan>%player% joined! :)"
    set title of player's scoreboard to "<orange>Testing Server"
    set line 1 of player's scoreboard to ""
    set line 2 of player's scoreboard to ""
    set line 3 of player's scoreboard to ""
    set line 4 of player's scoreboard to ""
    set line 5 of player's scoreboard to "<green>%player%"
    set line 6 of player's scoreboard to ""
    while player is online:
        if player is in world "pvp":
            set line 3 of player's scoreboard to "Gold: <gold>%{gold::%player%}%g"
            if {status_timer::%player%} is greater than 0:
                set line 1 of player's scoreboard to "Status: <red>Fighting"
            else:
                set line 1 of player's scoreboard to "Status: <light green>Idle"
        wait 1 second

on quit:
    set the quit message to "<light cyan>%player% left! :("

# removes hunger
on food bar change:
    cancel event

# stop player from dropping items
on drop:
    cancel event

# can't break blocks in survival
on break:
    if gamemode of player is survival:
        cancel event

on damage:
    if victim is in world "main":
        cancel event
    else if victim is in world "pvp":
        if y coordinate of victim is above -44:
            cancel event
        else:
            set {status_timer::%victim%} to 5
            set {status_timer::%attacker%} to 5

# stops player from shooting bows in lobby of "pvp"
on shoot:
    if projectile is an arrow:
        if y coordinate of projectile is above -44:
            cancel event

# so /spawn command works correctly
on respawn:
    if player is in world "main":
        set respawn location to location at 0.5, -60, 0.5
    else if player is in world "pvp":
        set respawn location to location at 0.5, -43, 0.5

on rightclick on sign:
    # lobby
    if line 2 of block is "PvP Arena":
        teleport player to location at 0.5, -43, 0.5 in world "pvp"
        send "<light green>Welcome to The PVP Arena" to player
    else if line 2 of block is "Parkour":
        send "Parkour Comming Soon..." to player
    else if line 2 of block is "CTF":
        send "CTF Comming Soon..." to player
    # the pit
    else if line 2 of block is "Lobby":
        teleport player to location 0.5, -60, 0.5 in world "main"
    else if line 2 of block is "Knight":
        remove all items from player's inventory
        remove swiftness from the player
        set slot 40 of player to unbreakable shield
        set slot 0 of player to unbreakable iron sword
        set slot 1 of player to unbreakable iron axe
        set helmet of player to unbreakable iron helmet
        set chestplate of player to unbreakable iron chestplate
        set leggings of player to unbreakable iron leggings
        set boots of player to unbreakable iron boots
        heal player
        send "<light green>Knight Kit Equipped!"
    else if line 2 of block is "Ninja":
        remove all items from player's inventory
        remove swiftness from the player
        apply swiftness 2 to the player for 1 day
        set slot 40 of player to unbreakable shield
        set slot 0 of player to unbreakable diamond sword
        set helmet of player to unbreakable leather helmet
        set chestplate of player to unbreakable iron chestplate
        set leggings of player to unbreakable leather leggings
        set boots of player to unbreakable leather boots
        heal player
        send "<light green>Ninja Kit Equipped!"
    else if line 2 of block is "Archer":
        remove all items from player's inventory
        remove swiftness from the player
        set slot 40 of player to unbreakable shield
        set slot 0 of player to unbreakable iron sword
        set slot 1 of player to unbreakable bow
        set slot 2 of player to 24 arrows
        set helmet of player to unbreakable iron helmet
        set chestplate of player to unbreakable iron chestplate
        set leggings of player to unbreakable iron leggings
        set boots of player to unbreakable iron boots
        heal player
        send "<light green>Archer Kit Equipped!"

# clears player inventory when switching from "pvp" to "main" world
on player world change:
    if player is in world "main":
        remove all items from player's inventory
        remove swiftness from the player
        set line 1 of player's scoreboard to ""
        set line 2 of player's scoreboard to ""
        heal player

every 1 second:
    loop all players:
        subtract 1 from {status_timer::%loop-player%}        
